#
# Demo the "with arrows" plot style
# This style differs from "with vectors" as follows:
#   plot FOO using (x):(y):(deltax):(deltay) with vectors
#   plot BAZ using (x):(y):(length):(angle) with arrows
#
set sample 11
set isosample 11
set urange [10:360]; set xrange [0:360]
set vrange [1: 10]; set yrange [0: 10]
unset key
set border 3
set tics nomirror rangelimit

set title "plot '++' using x:y:len:angle with arrows\n".\
          "    len = sqrt(x+y)      angle = x + 3*y"

set style arrow 1 noborder
plot '++' using 1:2:(sqrt($1+$2)):($1 + $2*3) with arrows as 1

pause -1 "Hit <cr> to continue"

set style arrow 2 noborder lw 2 lc palette z
set cbrange [0:12]
set palette cubehelix
f(x,y) = sqrt((x-150)*(x-150)/400. + (y-6)*(y-6))

set title "plot ... using x:y:len:angle:as:color with arrows arrowstyle variable"
plot '++' using 1:2:(sqrt($1+$2)):($1 + $2*3):(2):(f($1,$2)) with arrows as variable

pause -1 "Hit <cr> to continue"

reset
