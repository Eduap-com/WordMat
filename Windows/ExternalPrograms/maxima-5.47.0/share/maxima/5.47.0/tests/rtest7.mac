/*************** -*- Mode: MACSYMA; Package: MAXIMA -*-  ******************/
/***************************************************************************
***                                                                    *****
***     Copyright (c) 1984 by William Schelter,University of Texas     *****
***     All rights reserved                                            *****
***************************************************************************/


kill(all);
done$
linechar:%t;
%t$
atvalue(f(x,y),[x = 0,y = 1],a^2);
a^2$
atvalue('diff(f(x,y),x),x = 0,y+1);
\@2+1$
printprops(all,atvalue);
done$
diff(4*f(x,y)^2-u(x,y)^2,x);
8*f(x,y)*'diff(f(x,y),x,1)-2*u(x,y)*'diff(u(x,y),x,1)$
at(%,[x = 0,y = 1]);
16*a^2-2*u(0,1)*(''(at('diff(u(x,y),x,1),[x = 0,y = 1])))$
atvalue(f(x,y),[x = 0,y = 1],a^2);
a^2$
atvalue('diff(f(x,y),x),x = 0,y+1);
\@2+1$
printprops(all,atvalue);
done$
diff(4*f(x,y)^2-u(x,y)^2,x);
8*f(x,y)*'diff(f(x,y),x,1)-2*u(x,y)*'diff(u(x,y),x,1)$
at(%,[x = 0,y = 1]);
16*a^2-2*u(0,1)*(''(at('diff(u(x,y),x,1),[x = 0,y = 1])))$
listofvars(f(x[1]+y)/g^(2+a));
[g,a,x[1],y]$
coeff(2*a*tan(x)+tan(x)+b = 5*tan(x)+3,tan(x));
2*a+1 = 5$
coeff(y+x*%e^x+1,x,0);
y+1$
a*x+b*x+5;
b*x+a*x+5$
ratcoef(%,a+b);
x$
islinear(exp,var):=block([c],c:bothcoef(rat(exp,var),var),
	 is(freeof(var,c) and c[1] # 0));
islinear(exp,var):=block([c],c:bothcoef(rat(exp,var),var),
	 is(freeof(var,c) and c[1] # 0))$
islinear((r^2-(x-r)^2)/x,x);
true$
(a+b)^4*(1+x*(2*x+(c+d)^2));
(b+a)^4*(x*(2*x+(d+c)^2)+1)$
(linenum:0,isolate(%,x));
%t2*(x*(2*x+%t1)+1)$
ratexpand(%);
2*%t2*x^2+%t1*%t2*x+%t2$
ev(%);
2*(b+a)^4*x^2+(b+a)^4*(d+c)^2*x+(b+a)^4$
%t1;
(d+c)^2;
%t2;
(b+a)^4;
(a+b)*(x+a+b)^2*%e^(x^2+a*x+b);
(b+a)*(x+b+a)^2*%e^(x^2+a*x+b)$
block([linenum:2],ev(isolate(%,x),exptisolate:true));
%t3*%t4*(x+%t3)^2*%e^(x^2+a*x)$
%t3;
b+a;
%t4;
%e^b;
exp1:integrate(1/(x^3+2),x);
  -log(x^2-2^(1/3)*x+2^(2/3))/(6*2^(2/3))+atan((2*x-2^(1/3))/(2^(1/3)*sqrt(3)))
					/(2^(2/3)*sqrt(3))
				       +log(x+2^(1/3))/(3*2^(2/3))$
/* got this at one point but it differentiates ok
  -2^(1/3)*log(2^(2/3)*x^2-2*x+2*2^(1/3))/12+2^(1/3)*atan((2*2^(2/3)*x-2)/(2*sqrt(3)))/(2*sqrt(3))+2^(1/3)*log(x+2^(1/3))/6; */
radcan(diff(%,x));
1/(x^3+2);
block([linenum:4],pickapart(exp1,1));
%t7+%t6+%t5$
gamma(7/2);
15*sqrt(%pi)/8$
numfactor(%);
15/8$
'diff(y,x,2)+'diff(y,z,3)*2+'diff(y,x,1)*x^2;
2*'diff(y,z,3)+'diff(y,x,2)+x^2*'diff(y,x,1)$
derivdegree(%,y,x);
2$
(%i*v+u)/(f+%i*e)+%e^(%i*alpha);
(%i*v+u)/(f+%i*e)+%e^(%i*alpha)$
realpart(%);
(e*v+f*u)/(f^2+e^2)+cos(alpha)$
rectform(sin(2*%i+x));
cosh(2)*sin(x)+%i*sinh(2)*cos(x)$
polarform(%);
sqrt(cosh(2)^2*sin(x)^2+sinh(2)^2*cos(x)^2)
 *%e^(%i*atan2(sinh(2)*cos(x),cosh(2)*sin(x)))$
rectform(log(3+4*%i));
log(5)+%i*atan(4/3)$
polarform(%);
sqrt(log(5)^2+atan(4/3)^2)*%e^(%i*atan(atan(4/3)/log(5)))$

%emode:false;
false;
ev(rectform((2+3.5*%i)^0.25),numer);
0.3682587918166914*%i+1.368266274774369;
polarform(%);
1.416956998795383*%e^(0.2629125531370934*%i);
/* %emode:false is needed to keep above answer from being converted out of polar form 
   before being compared to result of polarform() */
%emode:true;
true;

delete(sin(x),x+sin(x)+y);
y+x$

/* SF bug 2014436 */
at(f(x),[]);
f(x)$

/* simplification of 'at' */

kill (f, g, h, u, v, w, x, y, z, a, b, c);
done;

at (f (x, y, z), x = a);
f (a, y, z);

'(at (f (x, y, z), x = a));
at (f (x, y, z), x = a);

'('at (f (x, y, z), x = a));
'at (f (x, y, z), [x = a]);

at (f (x, y), z = b);
f (x, y);

'(at (f (x, y), z = b));
at (f (x, y), z = b);

'('at (f (x, y), z = b));
f (x, y);

at ('f (x, y), [x = a, y = b]);
'f (a, b);

'(at ('f (x, y), [x = a, y = b]));
at ('f (x, y), [x = a, y = b]);

'('at ('f (x, y), [x = a, y = b]));
'at ('f (x, y), [x = a, y = b]);

at (diff (f (x, y), x, 1, y, 1), [x = a, y = b]);
'at ('diff (f (x, y), x, 1, y, 1), [x = a, y = b]);

at (diff (f (x, y), x, 1, y, 1), x = a);
'at ('diff (f (x, y), x, 1, y, 1), [x = a]);

at (at (diff (f (x, y), x, 1, y, 1), [x = a, y = b]), a = c);
'at ('diff (f (x, y), x, 1, y, 1), [x = c, y = b]);

at (at (diff (f (x, y), x, 1, y, 1), [x = a, y = b]), c = z);
'at ('diff (f (x, y), x, 1, y, 1), [x = a, y = b]);

depends (x, [y, z]);
[x(y, z)];

at (diff (x, y), [u = 1, v = 2, w = 3, z = 4]);
'at ('diff (x, y, 1), [z = 4]);

at (sum (f(a), a, 1, b), [a = x]);
'sum (f(a), a, 1, b);

at (sum (f(a), a, 1, b), [b = x]);
'sum (f(a), a, 1, x);

'('at ('sum (f(a), a, 1, b), [b = x]));
'at ('sum (f(a), a, 1, b), [b = x]);

at ('integrate (f(x), x), x = y);
'at ('integrate (f(x), x), [x = y]);

at ('integrate (f(x), x, a, b), x = y);
'integrate (f(x), x, a, b);

at ('laplace (f(t), t, s), [t = a, s = b]);
'laplace (f(t), t, b);

a : at (taylor (f(x), x, 0, 3), [x = 7/5]);
(343*('at('diff(f(x),x,3),x = 0)))/750+(49*('at('diff(f(x),x,2),x = 0)))/50
                                      +(7*('at('diff(f(x),x,1),x = 0)))/5+f(0)$

b : subst (f = cos, a);
(343*('at('diff(cos(x),x,3),x = 0)))/750
 +(49*('at('diff(cos(x),x,2),x = 0)))/50+(7*('at('diff(cos(x),x,1),x = 0)))/5
 +1$

ev (b, nouns);
1/50;

(taylor (cos(x), x, 0, 3), ev (%%, x=7/5));
''(rat(1/50)); /* not a vanilla 1/50 ... */

kill(all);
done;

at('at('diff(f(x,a),x,1),x=y),a=z);
'at('diff(f(x,z),x,1),x=y);

at('at('diff(f(x,a),x,1),x=y),y=z);
'at('diff(f(x,a),x,1),x=z);

at(diff(f(x),x),s=0);
'diff(f(x),x,1);

at('diff(f(x,n),x,m),n=2);
'diff(f(x,2),x,m);

at('diff(f(x,n),x,m),m=2);
'diff(f(x,n),x,2);

/* from mailing list 2015-12-08: "Evaluate derivative in Maxima" */

(kill(all),
 f1(x):=diff(f(x),x,1),
 f2(x):=diff(f(x),x,2),
 x0(s):=mu+s*at(f1(x),x=mu)/(1/sigma^2-s*at(f2(x),x=mu)),
 g(s,x):=s*f(x)-1/2*((x-mu)/sigma)^2,
 g2(s,x):=diff(g(s,x),x,2),
 g0(s):=at(g(s,x),x=x0(s)),
 g20(s):=at(g2(s,x),x=x0(s)),
 h(s):=1/sigma*exp(g0(s))*sqrt(1/abs(g20(s))),
 at(diff(h(s),s,1),s=0));
(sigma*abs(sigma)*('at('diff(f(x),x,2),x = mu)))/2+(f(mu)*abs(sigma))/sigma$

(h(s):=s*(at(f1(x),x=mu)),
 at(diff(h(s),s),s=0));
'at('diff(f(x),x,1), x=mu);

/* SF bug #1085: "Function \"at\" is overly cautious" */

at('integrate(f(t),t,0,x),x=0);
0;

at('integrate(f(t),t,x,y),[x=a, y=b]);
'integrate(f(t),t,a,b);

/* SF bug #2302: "'at' applied to definite integral" */

at(integrate(f(t),t,0,x),x=0);
0;

/* SF bug #3607: "  printprops displays matchdeclare properties incorrectly when value is a lambda" */

apply (kill, propvars (matchdeclare));
done;

matchdeclare (aa, all, bb, integerp, cc, lambda([x], floatnump(x) and x > 0));
done;

printprops (all, matchdeclare);
[all(aa), integerp(bb), lambda([x], floatnump(x) and (x > 0))(cc)];

